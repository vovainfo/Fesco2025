#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Отбор.Свойство("Склад") Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Склад", Параметры.Отбор.Склад);
	Иначе
		ВызватьИсключение НСтр("ru='Не задан склад.'"); 
	КонецЕсли;
	
	
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

// Код процедур и функций

#КонецОбласти
#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДобавитьНоменклатуру(Элементы.Список.ТекущиеДанные.Ссылка);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыбранные
&НаКлиенте
Процедура ВыбранныеПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("СправочникСсылка.Номенклатура") Тогда
		ДобавитьНоменклатуру(ПараметрыПеретаскивания.Значение[0]);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти



#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОтправитьВДокумент(Команда)
	МассивВозврата = Новый Массив;
	Для Каждого Элемент Из Выбранные Цикл
		НовыйЭлемент = Новый Структура("Номенклатура, Количество", 
									Элемент.Номенклатура, Элемент.Количество);
		МассивВозврата.Добавить(НовыйЭлемент);
	КонецЦикла;
	ОповеститьОВыборе(МассивВозврата);
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции
// Добавляет номенклатуру в список выбранных.
//
// Параметры:
//   Номенклатура - СправочникСсылка.Номенклатура - добавляемая номенклатура
&НаСервере
Процедура ДобавитьНоменклатуру(Номенклатура)
	// была бы БСП - использовал бы ЗначениеРеквизитаОбъекта, 
	// но БСП нет, а Номенклатура простой объект, легковесный.
	//@skip-check reading-attribute-from-database
	Если Номенклатура.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	НайденныеСтроки = Выбранные.НайтиСтроки(Новый Структура("Номенклатура", Номенклатура));
	Если НайденныеСтроки.Количество() > 0 Тогда
		НайденныеСтроки[0].Количество = НайденныеСтроки[0].Количество + 1;
	Иначе
		НоваяСтрока = Выбранные.Добавить();
		НоваяСтрока.Номенклатура = Номенклатура;
		НоваяСтрока.Количество = 1; 
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

